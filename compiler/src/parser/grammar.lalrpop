use std::str::FromStr;
use super::syntax::*;
use crate::util::loc::{WithLoc, Span, SrcLoc};

grammar;

// Expressions

L<T>: WithLoc<T> = {
    <start: @L> <val: T> <end: @R> => WithLoc::known(val, Span::from_offset_bytes(start, end))
};

WithSemi<T>: T = {
    <stmt:T> Semi => stmt,
};

pub Ident: Ident = {
    <s:r"[a-zA-Z_][a-zA-Z0-9_]*\??"> => Ident(s.to_string()),
};

pub LitNum: LitNum = {
    <s:r"[0-9]+"> => LitNum(i32::from_str(s).unwrap()),
};

pub LitStr: LitStr = {
    <s:r#""[^"]*""#> => LitStr(s[1..s.len()-1].to_string()),
};

pub FunCall: FunCall<SrcLoc> = {
    <name:L<Ident>> <args: L< <arg:SimpleExpr+> >> => FunCall { name, args },
};

Semi: () = {
    ";" => (),
};

pub SimpleExpr: Expr<SrcLoc> = {
    <n:L<LitNum>> => Expr::LitNum(n),
    <s:L<LitStr>> => Expr::LitStr(s),
    <i:L<Ident>> => Expr::Ident(i),
    "(" <e:Expr> ")" => e,
};

pub Expr: Expr<SrcLoc> = {
    <e:SimpleExpr> => e,
    <f:FunCall> => Expr::FunCall(f),
};

// Statements

pub TopLevelStmt: TopLevelStmt<SrcLoc> = {
    <f: L<FunDef>> => TopLevelStmt::FunDef(f),
};

pub FunDef: FunDef<SrcLoc> = {
    "fn" 
        <name:L<Ident>> 
        <params: L< <p:L<Ident>*> >>
        <preds: L< <p:Pred*> >>
        <body: L<Block>> => FunDef { name, params, preds, body },
};

Pred: Expr<SrcLoc> = {
    ":" <pred:Expr> => pred,
};

Block: Vec<FunStmt<SrcLoc>> = {
    "{" <stmts: FunStmt* > "}" => stmts,
};

pub FunStmt: FunStmt<SrcLoc> = {
    <stmt:L<WithSemi<Return>>> => FunStmt::Return(stmt),
    <stmt:L<WithSemi<Assignment>>> => FunStmt::Assignment(stmt),
    <stmt:L<WithSemi<LetDecl>>> => FunStmt::LetDecl(stmt),
    <stmt:L<While>> => FunStmt::While(stmt),
    <stmt:L<If>> => FunStmt::If(stmt),
};

pub Return: Return<SrcLoc> = {
    "return" <e:Expr> => Return(e),
};

pub Assignment: Assignment<SrcLoc> = {
    <name:L<Ident>> "=" <value:Expr> => Assignment { name, value },
};

pub LetDecl : LetDecl<SrcLoc> = {
    "let" <name:L<Ident>> "=" <value:Expr> => LetDecl { name, value },
};

pub While: While<SrcLoc> = {
    "while" <condition:Expr> <body:L<Block>> => While { condition, body },
};

pub If: If<SrcLoc> = {
    <bare:IfBare> => bare,
    <full:IfFull> => full,
};

pub IfBare: If<SrcLoc> = {
    "if" <condition:Expr> <then:L<Block>> => If { condition, then, else_: None },
};

pub IfFull: If<SrcLoc> = {
    "if" <condition:Expr> <then:L<Block>> "else" <else_:L<Block>> => If { condition, then, else_: Some(else_) },
};

pub Module: Module<SrcLoc> = {
    <stmts: L< <arg:TopLevelStmt*> >> => Module { statements: stmts },
};