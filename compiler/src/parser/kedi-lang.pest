ws = _{ (" " | "\r" | "\n" | "\t")+ }
WHITESPACE = _{ ws }

semi = _{ ";" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Expressions

placeholder_identifier = { "_" }
value_identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }

func_call_arg_list = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
func_call = { value_identifier ~ func_call_arg_list }

number_literal = { ASCII_DIGIT+ }

string_literal = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

literal = { number_literal | string_literal }

op_initial_chars = @{ "+" | "-" | "*" | "/" | "%" | "!" | "<" | ">" | "&" | "|" | "?" }
op_rest = @{ op_initial_chars | "=" | "." } 
operator = @{ op_initial_chars ~ op_rest* }

pl_expr = { 
    func_call | value_identifier | placeholder_identifier | literal |
    bracket_expr
} 

bracket_expr = { "(" ~ expr ~ ")" }

op_expr = { pl_expr ~ operator ~ expr }

expr = {
    op_expr |
    pl_expr
}

if_stmt = { "if" ~ expr ~ block ~ ("else" ~ block)? }
 
fun_arg = { value_identifier ~ ":" ~ expr }
fun_arg_list = { "(" ~ (fun_arg ~ ("," ~ fun_arg)*)? ~ ")" }
fun_decl = {
    "fn" ~ value_identifier 
        ~ fun_arg_list ~ ":" ~ expr ~ block
}

let_decl = { "let" ~ value_identifier ~ "=" ~ expr ~ semi }

assignment = { value_identifier ~ "=" ~ expr ~  semi }

inv = { "@" ~ expr ~ semi }

while = { "while" ~ expr ~ block }

return = { "return" ~ expr ~ semi }

top_level_stmt = { fun_decl }

fun_stmt = {
    let_decl |
    assignment |
    inv |
    return |
    while | 
    raw_wasm
}

raw_wasm = { "%wasm" ~ value_identifier_list ~ value_identifier_list ~ rest ~ ";" }
value_identifier_list = { "[" ~ value_identifier ~ ("," ~ value_identifier)* ~ "]" }

rest = { (!(";") ~ ANY)+ }

// File

block = { "{" ~ fun_stmt* ~ "}" }

module = { SOI ~ top_level_stmt* ~ EOI }
